apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  jvm {
    withJava()
  }
  js {
    configure([compilations.main, compilations.test]) {
      tasks.getByName(compileKotlinTaskName).kotlinOptions {
        moduleKind = 'umd'
        sourceMap = true
        metaInfo = true
      }
    }
    nodejs()
    browser()
  }

  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlin.stdlib.common
        api project(':wire-runtime')
      }
    }
    jvmMain {
      dependencies {
        api deps.kotlin.stdlib.jdk8
        implementation deps.okio.jvm
        api deps.guava
      }
    }
    jvmTest {
      dependencies {
        implementation project(':wire-test-utils')
        implementation deps.junit
        implementation deps.kotlin.test.junit
        implementation deps.assertj
        implementation deps.jimfs
      }
    }
    jsMain {
      dependencies {
        api deps.kotlin.stdlib.js
      }
    }
    jsTest {
      dependencies {
        implementation deps.kotlin.test.js
      }
    }
  }
}

jar {
  manifest {
    attributes('Automatic-Module-Name': 'wire-schema')
  }
}

apply from: "$rootDir/gradle/gradle-mvn-mpp-push.gradle"

publishing {
  // Use default artifact name for the JVM target
  publications {
    kotlinMultiplatform {
      artifactId = 'wire-schema-multiplatform'
    }
    jvm {
      artifactId = 'wire-schema'
    }
  }
}
