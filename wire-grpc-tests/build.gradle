buildscript {
  dependencies {
    classpath deps.protobuf.gradlePlugin
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'ru.vyarus.animalsniffer'
apply plugin: 'com.google.protobuf'

protobuf {
  plugins {
    grpc {
      artifact = deps.grpc.genJava
    }
  }

  protoc {
    artifact = deps.protobuf.protoc
  }

  generateProtoTasks {
    ofSourceSet('test')*.plugins {
      // Apply the "grpc" plugin whose spec is defined above, without
      // options.  Note the braces cannot be omitted, otherwise the
      // plugin will not be added. This is because of the implicit way
      // NamedDomainObjectContainer binds the methods.
      grpc {}
    }
  }
}

sourceSets {
  test.java.srcDirs += 'build/generated/source/proto/test/grpc'
  test.java.srcDirs += 'build/generated/source/proto/test/java'
  test.java.srcDirs += 'src/test/proto-grpc'
}

jar {
  manifest {
    attributes('Automatic-Module-Name': 'wire-grpc-tests')
  }
}

animalsniffer {
  sourceSets = [sourceSets.main]
  ignore 'com.squareup.wire.internal'
}

dependencies {
  implementation deps.wire.runtime
  implementation deps.wire.grpcClient
  implementation deps.okio.jvm
  if (JavaVersion.current().isJava9Compatible()) {
    // Workaround for @javax.annotation.Generated
    // see: https://github.com/grpc/grpc-java/issues/3633
    implementation 'javax.annotation:javax.annotation-api:1.3.1'
  }
  compileOnly deps.android
  testImplementation deps.junit
  testImplementation deps.assertj
  testImplementation deps.grpc.netty
  testImplementation deps.grpc.protobuf
  testImplementation deps.grpc.stub
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}
