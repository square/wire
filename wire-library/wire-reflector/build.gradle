apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  jvm {
    withJava()
  }

  sourceSets {
    jvmMain {
      kotlin.srcDir("$buildDir/wire")
      dependencies {
        api project(':wire-runtime')
        api project(':wire-grpc-client')
        api project(':wire-schema')
        implementation deps.okio.jvm
        api deps.guava
        implementation "io.grpc:grpc-protobuf:1.21.0"
        implementation "com.google.protobuf:protoc:3.6.1"
      }
    }
    jvmTest {
      dependencies {
        implementation project(':wire-test-utils')
        implementation deps.junit
        implementation deps.kotlin.test.junit
        implementation deps.assertj
        implementation deps.jimfs
      }
    }
  }
}

configurations {
  generateReflectionProtosClasspath
}

dependencies {
  generateReflectionProtosClasspath project(":wire-compiler")
}

task generateReflectionProtos(type: JavaExec) {
  main = 'com.squareup.wire.WireCompiler'
  classpath = configurations.generateReflectionProtosClasspath
  args(
          "--proto_path=$projectDir/src/jvmMain/proto",
          "--kotlin_out=$buildDir/wire",
          "grpc/reflection/v1alpha/reflection.proto"
  )
}

compileKotlinJvm {
  dependsOn generateReflectionProtos
}
