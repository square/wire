apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
  jvm {
    withJava()
    compilations.create('kotlinInteropTest') {
      defaultSourceSet {
        kotlin.srcDirs += 'src/jvmKotlinInteropTest/proto-kotlin'
        dependencies {
          implementation(compilations.main.compileDependencyFiles)
          implementation deps.kotlin.test.junit
          implementation deps.assertj
        }
      }
      tasks.create('jvmKotlinInteropTest', Test) {
        classpath = compileDependencyFiles + runtimeDependencyFiles + output.allOutputs
        testClassesDirs = output.classesDirs
      }
      jvmTest.dependsOn jvmKotlinInteropTest
    }
    compilations.create('kotlinAndroidTest') {
      defaultSourceSet {
        kotlin.srcDirs += 'src/jvmKotlinAndroidTest/proto-kotlin'
        dependencies {
          implementation(compilations.main.compileDependencyFiles)
          compileOnly deps.android
          implementation deps.kotlin.test.junit
          implementation deps.assertj
        }
      }
      tasks.create('jvmKotlinAndroidTest', Test) {
        classpath = compileDependencyFiles + runtimeDependencyFiles + output.allOutputs
        testClassesDirs = output.classesDirs
      }
      jvmTest.dependsOn jvmKotlinAndroidTest
    }
  }
  js {
    configure([compilations.main, compilations.test]) {
      tasks.getByName(compileKotlinTaskName).kotlinOptions {
        moduleKind = 'umd'
        sourceMap = true
        metaInfo = true
      }
    }
    nodejs()
  }
  iosX64()
  iosArm64()
  // Required to generate tests tasks: https://youtrack.jetbrains.com/issue/KT-26547
  linuxX64()
  macosX64()
  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlin.stdlib.common
        api project(':wire-runtime')
        api project(':wire-grpc-client')
      }
    }
    commonTest {
      kotlin.srcDirs += 'src/commonTest/proto-kotlin'
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.annotations
      }
    }
    jvmMain {
      dependencies {
        api deps.kotlin.stdlib.jdk6
      }
    }
    jvmTest {
      kotlin.srcDirs += 'src/jvmTest/proto-kotlin'
      kotlin.srcDirs += 'src/jvmTest/proto-java'
      dependencies {
        implementation project(':wire-test-utils')
        implementation deps.assertj
        implementation deps.kotlin.test.junit
        implementation deps.jimfs
      }
    }
    jsMain {
      dependencies {
        api deps.kotlin.stdlib.js
      }
    }
    jsTest {
      dependencies {
        implementation deps.kotlin.test.js
      }
    }
    nativeMain {
      dependsOn commonMain
    }
    nativeTest {
      dependsOn commonTest
    }

    configure([iosX64Main, iosArm64Main, linuxX64Main, macosX64Main]) {
      dependsOn nativeMain
    }
    configure([iosX64Test, iosArm64Test, linuxX64Test, macosX64Test]) {
      dependsOn nativeTest
    }
    all {
      languageSettings {
        useExperimentalAnnotation 'kotlin.Experimental'
      }
    }
  }
}

apply from: 'jvm.gradle'

task cleanGeneratedTests(type: Delete) {
  delete '../wire-grpc-tests/src/test/proto-grpc',
         'wire-moshi-adapter/src/test/java/com/squareup/wire/protos',
         '../wire-tests/src/commonTest/proto-kotlin',
         '../wire-tests/src/jvmJavaAndroidCompactTest/proto-java',
         '../wire-tests/src/jvmJavaAndroidTest/proto-java',
         '../wire-tests/src/jvmJavaCompactTest/proto-java',
         '../wire-tests/src/jvmJavaNoOptionsTest/proto-java',
         '../wire-tests/src/jvmJavaPrunedTest/proto-java',
         '../wire-tests/src/jvmKotlinAndroidTest/proto-kotlin',
         '../wire-tests/src/jvmKotlinInteropTest/proto-kotlin',
         '../wire-tests/src/jvmJavaTest/proto-java'
}

afterEvaluate { project ->
  project.tasks.named("clean").configure { task ->
    task.dependsOn(cleanGeneratedTests)
  }
}
