buildscript {

  apply from: new File("wire-library/dependencies.gradle")

  ext.deps.wire = [
    'compiler': "com.squareup.wire:wire-compiler",
    'gradlePlugin': "com.squareup.wire:wire-gradle-plugin",
    'grpcClient': "com.squareup.wire:wire-grpc-client",
    'gsonSupport': "com.squareup.wire:wire-gson-support",
    'javaGenerator': "com.squareup.wire:wire-java-generator",
    'kotlinGenerator': "com.squareup.wire:wire-kotlin-generator",
    'moshiAdapter': "com.squareup.wire:wire-moshi-adapter",
    'runtime': "com.squareup.wire:wire-runtime",
    'schema': "com.squareup.wire:wire-schema",
    'testUtils': "com.squareup.wire:wire-test-utils",
  ]

  dependencies {
    classpath deps.plugins.kotlin
    classpath deps.plugins.shadow
    classpath deps.wire.gradlePlugin
    classpath deps.plugins.japicmp
    classpath deps.plugins.mavenPublish
    classpath deps.animalSniffer.gradle
    classpath deps.androidGradlePlugin
    classpath deps.protobuf.gradlePlugin
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

subprojects { project ->
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
}

apply from: 'gen-tests.gradle'

tasks.wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
