apply plugin: 'application'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = 'com.squareup.wire.prune.ProtoPruner'

jar {
  manifest {
    attributes('Automatic-Module-Name': 'proto-pruner')
  }
}

dependencies {
  implementation project(':wire-compiler')
  implementation project(':wire-schema')
  implementation deps.kotlin.stdlib.jdk8
  implementation deps.guava
  testImplementation deps.assertj
  testImplementation deps.junit
}

shadowJar {
  classifier = 'jar-with-dependencies'
}

artifacts {
  archives shadowJar
}

// The `shadow` plugin internally applies the `distribution` plugin and
// automatically adds tasks to create respective tar and zip artifacts.
// https://github.com/johnrengelman/shadow/issues/347#issuecomment-424726972
// https://github.com/johnrengelman/shadow/commit/a824e4f6e4618785deb7f084c4a80ce1b78fc4fd
shadowDistTar.enabled = false
shadowDistZip.enabled = false
configurations.archives.artifacts.removeAll { it.file =~ 'tar' || it.file =~ 'zip' }

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}
