import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
  ext.versions = [
      'kotlin': '1.3.41',
      'wire': '3.0.0',
      'ktor': '1.2.3',
      'tcnative': '2.0.25.Final',
  ]
  ext.deps = [
      'agp': 'com.android.tools.build:gradle:3.5.0',
      'kotlin': [
          'plugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
          'coroutines': "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0-RC",
          'coroutinesAndroid': "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0-RC",
      ],
      'androidx': [
          'appcompat': "androidx.appcompat:appcompat:1.0.2",
          'constraintLayout': "androidx.constraintlayout:constraintlayout:1.1.3",
          'ktx': 'androidx.core:core-ktx:1.0.2',
      ],
      'wire': [
          'runtime': "com.squareup.wire:wire-runtime-multiplatform:${versions.wire}",
          'grpcClient': "com.squareup.wire:wire-grpc-client:${versions.wire}",
          'gradlePlugin': "com.squareup.wire:wire-gradle-plugin:${versions.wire}",
          'compiler': "com.squareup.wire:wire-gradle-plugin:${versions.wire}",
      ],
      'misk': 'com.squareup.misk:misk:0.7.0',
  ]

  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath deps.agp
    classpath deps.kotlin.plugin
    classpath deps.wire.gradlePlugin
  }
}

allprojects { project ->
  repositories {
    google()
    jcenter()
  }
  project.tasks.withType(KotlinCompile).all {
    kotlinOptions.jvmTarget = "1.8"
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
