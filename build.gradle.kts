import com.diffplug.gradle.spotless.SpotlessExtension
import com.diffplug.spotless.LineEnding
import com.vanniktech.maven.publish.MavenPublishBaseExtension
import com.vanniktech.maven.publish.SonatypeHost
import kotlinx.validation.ApiValidationExtension
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent.FAILED
import org.gradle.api.tasks.testing.logging.TestLogEvent.PASSED
import org.jetbrains.dokka.gradle.DokkaTaskPartial
import org.gradle.api.tasks.testing.logging.TestLogEvent.SKIPPED
import org.gradle.api.tasks.testing.logging.TestLogEvent.STARTED
// import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootExtension
import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootPlugin
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
  dependencies {
    classpath(libs.pluginz.dokka)
    classpath(libs.pluginz.android)
    classpath(libs.pluginz.binaryCompatibilityValidator)
    classpath(libs.pluginz.kotlin)
    classpath(libs.pluginz.kotlinSerialization)
    classpath(libs.pluginz.shadow)
    classpath(libs.pluginz.spotless)
    classpath(libs.protobuf.gradlePlugin)
    classpath(libs.vanniktechPublishPlugin)
    classpath(libs.pluginz.buildConfig)
    classpath(libs.wire.gradlePlugin)
    classpath(libs.guava)
    classpath("org.ow2.asm:asm:9.5")
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
  }
}

rootProject.plugins.withType(NodeJsRootPlugin::class) {
  // 16+ required for Apple Silicon support
  // https://youtrack.jetbrains.com/issue/KT-49109#focus=Comments-27-5259190.0-0
  rootProject.extensions.getByType(NodeJsRootExtension::class).nodeVersion = "16.13.1"
}

apply(plugin = "com.vanniktech.maven.publish.base")
apply(plugin = "org.jetbrains.dokka")

allprojects {
  group = project.property("GROUP") as String
  version = project.property("VERSION_NAME") as String

  repositories {
    mavenCentral()
    google()
  }
}

subprojects {
  if (name != "wire-golden-files" &&
        name != "wire-tests-proto3-swift" &&
        name != "wire-tests-swift"
  ) {
    apply(plugin = "com.diffplug.spotless")
    configure<SpotlessExtension> {
      val licenseHeaderFile = rootProject.file("gradle/license-header.txt")
      kotlin {
        target("**/*.kt")
        // Spotless would be checking generated files by Gradle (type accessors) and was failing the build because of
        // this so ignore it. Probably related with how tests are run for the wire-gradle-plugin module.
        targetExclude("**/.gradle/**", "**/build/generated/source/wire/**")
        targetExcludeIfContentContains("// Code generated by Wire protocol buffer compiler")
        ktlint(libs.versions.ktlint.get()).editorConfigOverride(mapOf("ktlint_standard_filename" to "disabled"))
        trimTrailingWhitespace()
        endWithNewline()
        toggleOffOn()
        lineEndings = LineEnding.UNIX
        licenseHeaderFile(licenseHeaderFile)
      }
      java {
        target("**/*.java")
        // Spotless would be checking generated files by Gradle (type accessors) and was failing the build because of
        // this so ignore it. Probably related with how tests are run for the wire-gradle-plugin module.
        targetExclude("**/.gradle/**", "**/build/generated/source/wire/**")
        targetExcludeIfContentContains("// Code generated by Wire protocol buffer compiler")
        googleJavaFormat(libs.googleJavaFormat.get().version)
        trimTrailingWhitespace()
        endWithNewline()
        toggleOffOn()
        lineEndings = LineEnding.UNIX
        licenseHeaderFile(licenseHeaderFile)
      }
      format("Swift") {
        target("**/*.swift")
        targetExcludeIfContentContains("// Code generated by Wire protocol buffer compiler")
        lineEndings = LineEnding.UNIX
        licenseHeaderFile(licenseHeaderFile, "(@propertyWrapper|public |import |enum )")
      }
    }
  }

  // The `application` plugin internally applies the `distribution` plugin and
  // automatically adds tasks to create/publish tar and zip artifacts.
  // https://docs.gradle.org/current/userguide/application_plugin.html
  // https://docs.gradle.org/current/userguide/distribution_plugin.html#sec:publishing_distributions_upload
  plugins.withType(DistributionPlugin::class) {
    tasks.findByName("distTar")?.enabled = false
    tasks.findByName("distZip")?.enabled = false
    configurations["archives"].artifacts.removeAll {
      val file: File = it.file
      file.name.contains("tar") || file.name.contains("zip")
    }
  }

  tasks.withType<KotlinCompile>().configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
      // Disable optimized callable references. See https://youtrack.jetbrains.com/issue/KT-37435
      freeCompilerArgs += "-Xno-optimized-callable-references"
      freeCompilerArgs += "-Xjvm-default=all"
      // https://kotlinlang.org/docs/whatsnew13.html#progressive-mode
      freeCompilerArgs += "-progressive"
    }
  }

  tasks.withType<JavaCompile>().configureEach {
    sourceCompatibility = JavaVersion.VERSION_1_8.toString()
    targetCompatibility = JavaVersion.VERSION_1_8.toString()
    options.encoding = Charsets.UTF_8.toString()
  }

  tasks.withType<Test> {
    testLogging {
      events(STARTED, PASSED, SKIPPED, FAILED)
      exceptionFormat = TestExceptionFormat.FULL
      showStandardStreams = false
    }
  }
  if (!(
       project.name.endsWith("-swift") ||
         project.name.endsWith("-bom") ||
         project.name.endsWith("-benchmarks") ||
         project.name.contains("golden") ||
         project.name.contains("protoc") ||
         project.displayName.contains("sample")
      )
  ) {
    apply(plugin = "checkstyle")

    afterEvaluate {
      configure<CheckstyleExtension> {
        toolVersion = "7.7"
        sourceSets = listOf(project.extensions.getByType<SourceSetContainer>()["main"])
      }
    }

    apply(plugin = "org.jetbrains.dokka")

    tasks.withType<DokkaTaskPartial>().configureEach {
      outputDirectory.set(project.file("${project.rootDir}/docs/3.x"))
      dokkaSourceSets.configureEach {
          reportUndocumented.set(false)
          skipDeprecated.set(true)
          jdkVersion.set(8)
          perPackageOption {
            matchingRegex.set("com\\.squareup\\.wire.*\\.internal.*")
            suppress.set(true)
          }
      }
    }
  }
}

allprojects {
  tasks.withType<Jar>().configureEach {
    if (name == "jar") {
      manifest {
        attributes("Automatic-Module-Name" to project.name)
      }
    }
  }

  plugins.withId("com.vanniktech.maven.publish.base") {
    configure<PublishingExtension> {
      repositories {
        maven {
          name = "test"
          setUrl("file://${project.rootProject.layout.buildDirectory.dir("localMaven").get().asFile.path}")
        }
        /**
         * Want to push to an internal repository for testing?
         * Set the following properties in ~/.gradle/gradle.properties.
         *
         * internalUrl=YOUR_INTERNAL_URL
         * internalUsername=YOUR_USERNAME
         * internalPassword=YOUR_PASSWORD
         */
        val internalUrl = providers.gradleProperty("internalUrl")
        if (internalUrl.isPresent) {
          maven {
            name = "internal"
            setUrl(internalUrl)
            credentials(PasswordCredentials::class)
          }
        }
      }
    }

    configure<MavenPublishBaseExtension> {
      publishToMavenCentral(SonatypeHost.S01)
      val inMemoryKey = project.findProperty("signingInMemoryKey") as String?
      if (!inMemoryKey.isNullOrEmpty()) {
        signAllPublications()
      }
      pom {
        description.set("gRPC and protocol buffers for Android, Kotlin, and Java.")
        name.set(project.name)
        url.set("https://github.com/square/wire/")
        licenses {
          license {
            name.set("The Apache Software License, Version 2.0")
            url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
            distribution.set("repo")
          }
        }
        developers {
          developer {
            id.set("square")
            name.set("Square, Inc.")
          }
        }
        scm {
          url.set("https://github.com/square/wire/")
          connection.set("scm:git:https://github.com/square/wire.git")
          developerConnection.set("scm:git:ssh://git@github.com/square/wire.git")
        }
      }
    }
  }

  plugins.withType<com.android.build.gradle.BasePlugin>().configureEach {
    project.extensions.getByType<com.android.build.gradle.BaseExtension>().apply {
      compileSdkVersion(33)
      compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
      }
      defaultConfig {
        if (project.name.contains("app")) {
          applicationId("$group.${project.name}".replace(oldChar = '-', newChar = '.'))
        }
        minSdk = 28
        targetSdk = 33
        versionCode = 1
        versionName = "1.0"
      }
    }
  }
}

subprojects {
  plugins.withId("binary-compatibility-validator") {
    configure<ApiValidationExtension> {
      ignoredPackages += "grpc.reflection.v1alpha"
    }
  }
}

tasks.register("publishPluginToGradlePortalIfRelease") {
  val VERSION_NAME: String by project
  // Snapshots cannot be released to the Gradle portal. And we don't want to release internal square
  // builds.
  if (VERSION_NAME.endsWith("-SNAPSHOT") || VERSION_NAME.contains("square")) return@register

  dependsOn(":wire-gradle-plugin:publishPlugins")
}

apply(from = "gen-tests.gradle.kts")
