apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.squareup.wire'

protobuf {
  protoc {
    artifact = deps.protobuf.protoc
  }
}

wire {
  sourcePath {
    srcDir 'src/main/proto'
  }

  sourcePath {
    srcJar deps.protobuf.java
    include 'google/protobuf/descriptor.proto'
  }

  protoPath {
    srcJar 'src/main/proto/protos.jar'
    include 'squareup/geology/period.proto'
  }

  kotlin {
    javaInterop = true
    includes = [
            'squareup.proto2.kotlin.*',
            'squareup.proto3.kotlin.*',
            'google.protobuf.*',
    ]
  }

  java {
    includes = [
            'squareup.proto2.java.*',
            'squareup.proto3.java.*',
    ]
  }
}

sourceSets {
  test.java.srcDirs += 'build/generated/source/proto/main/java'
  test.java.srcDirs += 'build/generated/source/wire/'
}

dependencies {
  protobuf deps.wire.schema
  implementation deps.kotlin.stdlib.jdk8
  implementation deps.okio.jvm
  implementation deps.protobuf.java
  implementation deps.wire.runtime
  testImplementation deps.assertj
  testImplementation deps.junit
  testImplementation deps.protobuf.javaUtil
  testImplementation deps.wire.testUtils
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}

// Workaround the Gradle bug resolving multiplatform dependencies.
// https://youtrack.jetbrains.com/issue/KT-32239
configurations.all { configuration ->
  if (name.contains('kapt') || name.contains("wire") || name.contains("proto")) {
    attributes.attribute(Usage.USAGE_ATTRIBUTE,
            project.objects.named(Usage.class, Usage.JAVA_RUNTIME))
  }
}
