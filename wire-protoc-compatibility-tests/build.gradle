buildscript {
  dependencies {
    classpath deps.protobuf.gradlePlugin
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'ru.vyarus.animalsniffer'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.squareup.wire'

protobuf {
  protoc {
    artifact = deps.protobuf.protoc
  }
}

wire {
  proto3Preview = 'UNSUPPORTED'

  sourcePath {
    srcDir 'src/main/proto'
  }

  sourcePath {
    srcJar deps.protobuf.java
    include 'google/protobuf/descriptor.proto'
  }

  protoPath {
    srcJar 'src/main/proto/protos.jar'
    include 'squareup/geology/period.proto'
  }

  kotlin {
    javaInterop = true
  }
}

sourceSets {
  test.java.srcDirs += 'build/generated/source/proto/main/java'
  test.java.srcDirs += 'build/generated/source/wire/'
}

jar {
  manifest {
    attributes('Automatic-Module-Name': 'wire-protoc-compatibility-tests')
  }
}

animalsniffer {
  sourceSets = [sourceSets.main]
  ignore 'com.squareup.wire.internal'
}

dependencies {
  implementation deps.kotlin.stdlib.jdk8
  implementation deps.okio.jvm
  implementation deps.protobuf.java
  implementation deps.wire.runtime
  testImplementation deps.assertj
  testImplementation deps.junit
  testImplementation deps.moshi
  testImplementation deps.protobuf.javaUtil
  testImplementation deps.wire.moshiAdapter
  testImplementation deps.wire.testUtils
}

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
  }
}
