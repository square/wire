import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

import java.nio.file.Files
import java.nio.file.Paths
import java.util.stream.Collectors

buildscript {
  dependencies {
    classpath deps.plugins.kotlin
  }
  repositories {
    mavenCentral()
  }
}

def PROTOS = Files.find(
      Paths.get("${rootProject.rootDir}/wire-tests/src/commonTest/proto/java"),
      Integer.MAX_VALUE,
      { path, attrs -> attrs.isRegularFile() }
    )
    .map { path ->
      Paths.get("${rootProject.rootDir}/wire-tests/src/commonTest/proto/java").relativize(path)
    }
    .filter { path -> !path.startsWith("kotlin") }
    .map { path -> path.toString() }
    .filter { path -> path.endsWith(".proto") }
    .sorted()
    .collect(Collectors.toList())

configurations {
  wire.with {
    def platformAttr = Attribute.of('org.jetbrains.kotlin.platform.type', KotlinPlatformType.class)
    attributes.attribute(platformAttr, KotlinPlatformType.jvm)
  }
}

dependencies {
  wire project(':wire-compiler')
}

// JAVA

task generateJavaTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates Java classes from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/java',
      '--java_out=wire-tests/src/jvmJavaTest/proto-java',
      'google/protobuf/any.proto',
      'google/protobuf/descriptor.proto',
      '--excludes=squareup.options.*',
  ] + PROTOS
}

// NO OPTIONS

task generateNoOptionsTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates Java classes with no options from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/java',
      '--java_out=wire-tests/src/jvmJavaNoOptionsTest/proto-java',
      'google/protobuf/any.proto',
      'google/protobuf/descriptor.proto',
      '--excludes=google.protobuf.*',
      '--excludes=squareup.options.*',
  ] + PROTOS
}

// COMPACT

task generateCompactTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates compact Java classes from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/java',
      '--java_out=wire-tests/src/jvmJavaCompactTest/proto-java',
      '--compact',
      'all_types.proto'
  ]
}

// GSON (uses COMPACT)

task copyAllTypesTest(type: Copy) {
  from 'wire-tests/src/jvmTest/proto-java.compact/com/squareup/wire/protos/alltypes/AllTypes.java'
  into 'wire-gson-support/src/test/java/com/squareup/wire/protos/alltypes'
}
task copyRepeatedPackedAndMapTest(type: Copy) {
  from 'wire-tests/src/jvmTest/proto-java.compact/com/squareup/wire/protos/RepeatedPackedAndMap.java'
  into 'wire-gson-support/src/test/java/com/squareup/wire/protos'
}
task generateGsonTests(dependsOn: [copyAllTypesTest, copyRepeatedPackedAndMapTest]) {
  group = 'Generate Tests'
  description = 'Generates Java classes that use Gson from the test protos'
}

// INCLUDES / EXCLUDES

task generateIncludesExcludesTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates Java classes with included and excluded protos from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/java',
      '--java_out=wire-tests/src/jvmJavaPrunedTest/proto-java',
      '--includes=squareup.protos.roots.A,squareup.protos.roots.H',
      '--excludes=squareup.protos.roots.B',
      'roots.proto'
  ]
}

// ANDROID

task generateAndroidTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates Java classes for Android from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/java',
      '--java_out=wire-tests/src/jvmJavaAndroidTest/proto-java',
      '--android',
      'person.proto'
  ]
}

// ANDROID COMPACT

task generateAndroidCompactTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates compact Java classes for Android from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/java',
      '--java_out=wire-tests/src/jvmJavaAndroidCompactTest/proto-java',
      '--android',
      '--compact',
      'person.proto'
  ]
}

// KOTLIN

task generateKotlinTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates Kotlin classes from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/kotlin',
      '--kotlin_out=wire-tests/src/commonTest/proto-kotlin',
      'all_types.proto',
      'custom_options.proto',
      'deprecated.proto',
      'deprecated_enum.proto',
      'edge_cases.proto',
      'external_message.proto',
      'form.proto',
      'google/protobuf/any.proto',
      'google/protobuf/descriptor.proto',
      'map.proto',
      'no_fields.proto',
      'one_of.proto',
      'optional_enum.proto',
      'packed_encoding.proto',
      'person.proto',
      'redacted_one_of.proto',
      'redacted_test.proto',
      'same_name_enum.proto',
      'simple_message.proto',
      'to_string.proto',
      'unknown_fields.proto',
      'uses_any.proto',
  ]
}

// KOTLIN SERVICES

task generateKotlinServicesTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates Kotlin classes from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/kotlin',
      '--kotlin_out=wire-tests/src/jvmKotlinInteropTest/proto-kotlin',
      'service_kotlin.proto',
      'service_without_package.proto',
  ]
}

// KOTLIN ANDROID

task generateKotlinAndroidTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates Kotlin classes for Android from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/kotlin',
      '--kotlin_out=wire-tests/src/jvmKotlinAndroidTest/proto-kotlin',
      '--android',
      'person.proto'
  ]
}

// KOTLIN JAVA INTEROP

task generateKotlinJavaInteropTests(type: JavaExec) {
  group = 'Generate Tests'
  description = 'Generates Kotlin classes with Java interop from the test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
      '--proto_path=wire-tests/src/commonTest/proto/kotlin',
      '--kotlin_out=wire-tests/src/jvmKotlinInteropTest/proto-kotlin',
      '--java_interop',
      'all_types.proto',
      'deprecated.proto',
      'google/protobuf/any.proto',
      'google/protobuf/descriptor.proto',
      'map.proto',
      'no_fields.proto',
      'one_of.proto',
      'packed_encoding.proto',
      'percents_in_kdoc.proto',
      'person.proto',
      'redacted_one_of.proto',
      'repeated.proto',
      'same_name_enum.proto',
      'simple_message.proto',
      'unknown_fields.proto',
      'uses_any.proto',
  ]
}

// GRPC

def GRPC_PROTOS = Files.find(
      Paths.get("${rootProject.rootDir}/wire-grpc-tests/src/test/proto"),
      Integer.MAX_VALUE,
      { path, attrs -> attrs.isRegularFile() }
    )
    .map { path -> Paths.get("${rootProject.rootDir}/wire-grpc-tests/src/test/proto").relativize(path) }
    .filter { path -> !path.startsWith("kotlin") }
    .map { path -> path.toString() }
    .filter { path -> path.endsWith(".proto") }
    .sorted()
    .collect(Collectors.toList())

task generateGrpcTests(type: JavaExec) {
  group = 'Generate gRPC Tests'
  description = 'Generates Kotlin classes from the gRPC test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
          '--proto_path=wire-grpc-tests/src/test/proto',
          '--kotlin_out=wire-grpc-tests/src/test/proto-grpc',
          'routeguide/RouteGuideProto.proto'
  ] + GRPC_PROTOS
}

// Moshi Adapter

task generateMoshiAdapterCompactJavaTests(type: JavaExec) {
  group = 'Generate MoshiAdapter Compact Java Tests'
  description = 'Generates compat Java classes from the MoshiAdapter test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
          '--proto_path=wire-moshi-adapter/src/test/proto',
          '--java_out=wire-moshi-adapter/src/test/java',
          '--compact',
          'all_types.proto'
  ]
}

task generateMoshiAdapterJavaTests(type: JavaExec) {
  group = 'Generate MoshiAdapter Java Tests'
  description = 'Generates Java classes from the MoshiAdapter test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
          '--proto_path=wire-moshi-adapter/src/test/proto',
          '--java_out=wire-moshi-adapter/src/test/java',
          'collection_types.proto',
          'person_java.proto',
          'dinosaur_java.proto'
  ]
}

task generateMoshiAdapterInteropKotlinTests(type: JavaExec) {
  group = 'Generate MoshiAdapter Java interop Kotlin Tests'
  description = 'Generates Java interop Kotlin classes from the MoshiAdapter test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
          '--proto_path=wire-moshi-adapter/src/test/proto',
          '--kotlin_out=wire-moshi-adapter/src/test/java',
          '--java_interop',
          'person_java_interop_kotlin.proto',
          'dinosaur_java_interop_kotlin.proto'
  ]
}

task generateMoshiAdapterKotlinTests(type: JavaExec) {
  group = 'Generate MoshiAdapter Kotlin Tests'
  description = 'Generates Kotlin classes from the MoshiAdapter test protos'
  classpath = configurations.wire
  main = 'com.squareup.wire.WireCompiler'
  args = [
          '--proto_path=wire-moshi-adapter/src/test/proto',
          '--kotlin_out=wire-moshi-adapter/src/test/java',
          'person_kotlin.proto',
          'dinosaur_kotlin.proto'
  ]
}

task generateTests(dependsOn: [generateJavaTests, generateCompactTests, generateNoOptionsTests,
    generateGsonTests, generateIncludesExcludesTests, generateAndroidTests,
    generateAndroidCompactTests, generateKotlinTests, generateKotlinServicesTests,
    generateKotlinAndroidTests, generateKotlinJavaInteropTests, generateGrpcTests,
    generateMoshiAdapterCompactJavaTests, generateMoshiAdapterJavaTests,
    generateMoshiAdapterKotlinTests, generateMoshiAdapterInteropKotlinTests]) {
  group = 'Generate Tests'
  description = 'Generates all test classes'
}

task cleanGeneratedTests(type: Delete) {
  delete 'wire-grpc-tests/src/test/proto-grpc',
         'wire-moshi-adapter/src/test/java/com/squareup/wire/protos',
         'wire-tests/src/commonTest/proto-kotlin',
         'wire-tests/src/jvmJavaAndroidCompactTest/proto-java',
         'wire-tests/src/jvmJavaAndroidTest/proto-java',
         'wire-tests/src/jvmJavaCompactTest/proto-java',
         'wire-tests/src/jvmJavaNoOptionsTest/proto-java',
         'wire-tests/src/jvmJavaPrunedTest/proto-java',
         'wire-tests/src/jvmKotlinAndroidTest/proto-kotlin',
         'wire-tests/src/jvmKotlinInteropTest/proto-kotlin',
         'wire-tests/src/jvmJavaTest/proto-java'
}

project("wire-tests").afterEvaluate { project ->
  project.tasks.named("clean").configure { task ->
    task.dependsOn(cleanGeneratedTests)
  }
}
